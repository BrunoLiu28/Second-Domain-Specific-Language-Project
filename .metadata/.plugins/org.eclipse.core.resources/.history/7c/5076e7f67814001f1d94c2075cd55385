/*
 * generated by Xtext 2.34.0
 */
package org.xtext.example.mydsl.generator

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.AbstractGenerator
import org.eclipse.xtext.generator.IFileSystemAccess2
import org.eclipse.xtext.generator.IGeneratorContext
import simplemioModel.Model
import java.util.List
import simplemioModel.Event
import simplemioModel.ConditionalSensor
import simplemioModel.Sensor
import simplemioModel.Not
import simplemioModel.And
import simplemioModel.Or
import simplemioModel.Action
import java.util.ArrayList

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class SimpleMioGenerator extends AbstractGenerator {

	override void doGenerate(Resource resource, IFileSystemAccess2 fsa, IGeneratorContext context) {
		fsa.generateFile("program.aseba", toAsebaCode(resource.contents.head as Model));
	}
	
	def toAsebaCode(Model model) {
		'''
		var obstacle = 2000
		var line = 400
		var mic = 150
		var motor = 250
		
		onevent prox
		«generateEvents(filterEvent(model.getProgram().getEvents(), "obstacleturnline"))»
		
		onevent motor
		«generateEvents(filterEvent(model.getProgram().getEvents(), "motor"))»
		
		onevent buttons
		«generateEvents(filterEvent(model.getProgram().getEvents(), "button"))»
		
		onevent mic
		«generateEvents(filterEvent(model.getProgram().getEvents(), "sound"))»
		'''
	}
	
	def generateEvents(List<Event> events) {
		'''
			if 0 != 0 then
		«FOR e : events»
				elseif
			«generateEvent(e)»
		«ENDFOR»
			end
		'''
	}
	
	def generateEvent(Event event) {
		'''
		«generateConditionalSensor(event.getConditionalsensor())» then	
		«FOR action : event.getActions()»
			«generateAction(action)»
		«ENDFOR»
		'''
	}
	
	def generateConditionalSensor(ConditionalSensor cs) {
		'''
		«IF cs instanceof Sensor»
			«val intensity = if(cs.strength != null) cs.strength else 0»
			«IF cs.getSensorName() == "obstacle"»
				«IF cs.sensorSpecifier == "front"»
					prox.horizontal[1] > obstacle
						and prox.horizontal[2] > obstacle
						and prox.horizontal[3] > obstacle
				«ELSEIF cs.sensorSpecifier == "right"»
					prox.horizonal[3] > obstacle
						and prox.horizontal[4] > obstacle
				«ELSEIF cs.sensorSpecifier == "left"»
					prox.horizonal[0] > obstacle
						and prox.horizontal[1] > obstacle
				«ELSEIF cs.sensorSpecifier == "back"»
						prox.horizonal[5] > obstacle
						and prox.horizontal[6] > obstacle
				«ENDIF»
			«ELSEIF cs.getSensorName() == "line"»
				«IF cs.getSensorSpecifier() == "right"»
					prox.ground.delta[1] < line
				«ELSEIF cs.getSensorSpecifier() == "left"»
					prox.ground.delta[0] < line
				«ENDIF»
			«ELSEIF cs.getSensorName() == "sound"»
				mic.intensity > mic
			«ELSEIF cs.getSensorName() == "motor"»
				(motor.left.speed > 0 or motor.right.speed > 0)
			«ELSEIF cs.getSensorName() == "button"»
				«IF cs.getSensorSpecifier() == "center"»
					button.center > 0
				«ENDIF»
			«ENDIF»
		«ELSEIF cs instanceof Not»
			not («generateConditionalSensor(cs.getConditionalsensor())»)
		«ELSEIF cs instanceof And»
			(«generateConditionalSensor(cs.getLeft())») and («generateConditionalSensor(cs.getRight())»)
		«ELSEIF cs instanceof Or»
			(«generateConditionalSensor(cs.getLeft())») or («generateConditionalSensor(cs.getRight())»)
		«ENDIF»
		'''	
	}
	
	def generateAction(Action action) {
		'''
		«IF action.getActionName() == "move"»
			«IF action.getActionSpecifier() == "forward"»
				motor.left.target = motor
				motor.right.target = motor
			«ELSEIF action.getActionSpecifier() == "backward"»
				motor.left.target = -motor
				motor.right.target = -motor
			«ELSEIF action.getActionSpecifier() == "stop"»
				motor.left.target = 0
				motor.right.target = 0
			«ENDIF»
		«ELSEIF action.getActionName() == "turn"»
			«IF action.getActionSpecifier() == "right"»
				motor.left.target = motor
				motor.right.target = -motor
			«ELSEIF action.getActionSpecifier() == "left"»
				motor.left.target = -motor
				motor.right.target = motor
			«ENDIF»
		«ELSEIF action.getActionName() == "led"»
			«IF action.getActionSpecifier() == "red"»
				led.top(255, 0 ,0)
			«ELSEIF action.getActionSpecifier() == "green"»
				led.top(0, 255, 0)
			«ELSEIF action.getActionSpecifier() == "blue"»
				led.top(0, 0, 255)
			«ENDIF»
		«ENDIF»
		'''
	}
	
	// It is necessary to filter the events by their type because in aseba
	// it is only possible to have on onevent prox (onevent *event_name*)
	// So it is necessary to know what type of sensor each event uses
	
	def filterEvent(List<Event> events, String eventNames) {
		var res = new ArrayList()
		for (Event e : events) {
			var flattened = flatten(e.conditionalsensor)
			var isEvent = false
			for (Sensor cs : flattened){
				if (eventNames.contains(cs.sensorName)) {
					isEvent = true
				}
			}
			if (isEvent) res.add(e)
		}
		
		return res
	}
	
	def List<Sensor> flatten(ConditionalSensor cs) {
		var res = new ArrayList
		if (cs instanceof Sensor) {
			res.add(cs)
		} else if (cs instanceof Not) {
			res.addAll(flatten(cs.conditionalsensor))
		} else if (cs instanceof And) {
			res.addAll(flatten(cs.left))
			res.addAll(flatten(cs.right))
		} else if (cs instanceof Or) {
			res.addAll(flatten(cs.left))
			res.addAll(flatten(cs.right))
		}
		
		return res
	}
}
